Rule "no op":
    FILE:
        pass
;

Rule "Length limit":
    FILE:
        if LIKE(file.name, "^__.*__.py$"):  // e.g. __init__.py
            assert(file.lines < 10)
        else:
            assert(file.lines < 1000)
        ;
    CLASS:
        assert(class.lines < 300)
    FUNCTION:
        assert(function.lines < 60)
;

Rule "Names":
    FILE:
        assert(LIKE(file.name, "^[a-z0-9_]*.py$"))  // snake_case
    FUNCTION:
        assert(LIKE(function.name, "^[a-z0-9_]*$"))  // snake_case
    NAME:
        if OR(name.is_parent_file, AND(name.is_parent_class, name.parent_class.is_enum)):
            assert(LIKE(name.identifier, "^[A-Z0-9_]*$"))  // UPPER_SNAKE_CASE
        else:
            assert(LIKE(name.identifier, "^[a-z0-9_]*$"))  // snake_case
        ;
    CLASS:
        assert(LIKE(class.name, "^([A-Z][a-z0-9]+)+$"))  // CamelCase
;

Rule "File called by single class name":
    FILE:
        if file.is_single_class:
            assert(LOWER(SUB("(?!^)([A-Z]+)", "_\1", file.single_class.name)) = SUB(".py$", "", file.name))
        else:
            pass
        ;
;
